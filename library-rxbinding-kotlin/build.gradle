buildscript {
    ext.kotlin_version = '1.1.2-3'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 11
        versionName "2.0.4"
    }
    sourceSets{
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile project(path: ':library-rxbinding')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        'library-rxbinding-kotlin'(MavenPublication) {
            groupId 'com.github.arimorty'
            artifactId 'floatingsearchview-rxbinding-kotlin'
            version '2.0.5'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/library-rxbinding-kotlin-release.aar")
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

publishToMavenLocal.dependsOn(assemble)
